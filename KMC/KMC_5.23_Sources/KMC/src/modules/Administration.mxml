<?xml version="1.0" encoding="utf-8"?>
<modules:KmcModule xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:modules="com.kaltura.kmc.modules.*" 
				   xmlns:control="com.kaltura.kmc.modules.admin.control.*"
				   xmlns:view="com.kaltura.kmc.modules.admin.view.*" 
				   xmlns:model="com.kaltura.kmc.modules.admin.model.*" styleName="adminModuleMain" 
				   layout="vertical" minHeight="520" minWidth="950" 
				   creationComplete="creationCompleteHandler(event)" >
	<mx:Metadata>
		[ResourceBundle("admin")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.analytics.GoogleAnalyticsConsts;
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.kmc.business.JSGate;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.admin.control.events.GeneralEvent;
			import com.kaltura.kmc.modules.admin.control.events.ListItemsEvent;
			import com.kaltura.kmc.modules.admin.control.events.PartnerEvent;
			import com.kaltura.kmc.modules.admin.model.AdminModelLocator;
			import com.kaltura.kmc.modules.admin.view.IAdminSubtab;
			
			import mx.binding.utils.BindingUtils;
			import mx.events.FlexEvent;

			/**
			 * KMC is responsible to use the value of
			 * this const as the id of this module.
			 * */
			public static const NAME:String = "admin";
			
			
			[Bindable]
			/**
			 * module model
			 * */
			private var _model:AdminModelLocator = AdminModelLocator.getInstance();
			
			
			/**
			 * in case we want to start from a tab other than the
			 * first, name of the initial tab
			 * */
			private var _initialTab:String;
			
			
			/**
			 * indicates start() has finished executing so all initial
			 * data is present, as well as cretionComplete.
			 * */
			protected var _ready:Boolean = false;


			override public function getModuleName():String {
				return NAME;
			}




			override public function showSubtab(subtab:String, data:Object = null):void {
				// show the tab
				_initialTab = subtab;
				if (_ready) {
					switchToTab(subtab);
				}
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				var tabsToHide:Array = permissionManager.getRelevantSubTabsToHide(NAME);
				for each (var subtabName:String in tabsToHide) {
					var tabToRemove:DisplayObject = contentView.getChildByName(subtabName);
					if (tabToRemove) {
						contentView.removeChild(tabToRemove);
					}
				}
				
				BindingUtils.bindSetter(toggleLoading, _model, "isLoading");
				handleReady();
			}
			
			protected function toggleLoading(isIt:Boolean):void {
				this.enabled = !isIt;
				
				if (isIt) {
					cursorManager.setBusyCursor();
				}
				else {
					cursorManager.removeBusyCursor();
				}
			}


			override protected function start():void {
				Security.allowDomain("*");
				
//				// roles and permissions 
//				var tabsToHide:Array = permissionManager.getRelevantSubTabsToHide(NAME);
//				for each (var subtabName:String in tabsToHide) {
//					contentView.removeChild(this[subtabName]);
//				}
				GoogleAnalyticsTracker.getInstance().setBasePath(GoogleAnalyticsConsts.ADMIN);
				initModel();
				handleReady();
			}
			
			/**
			 * wait for both <code>start()</code> call and 
			 * <code>creationComplete</code> event, then start acting
			 * */
			private function handleReady():void {
				if (_ready ) {
					if (_initialTab) {
						switchToTab(_initialTab);
					}
					setModuleReady();
				}
				else {
					_ready = true;
				}
			}
			
			
			private function switchToTab(tab:String = ""):void {
				switch (tab) {
					case 'roles':
						secTln.selectedIndex = 1;
						roles.refreshData();
						GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.PAGE_VIEW + GoogleAnalyticsConsts.ADMIN_ROLES);
						break;
					case 'users':
					default:
						secTln.selectedIndex = 0;
						users.refreshData();
						GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.PAGE_VIEW + GoogleAnalyticsConsts.ADMIN_USERS);
						break;
				}
			}
			
			
			/**
			 * initialize administration model
			 * */
			private function initModel():void {
				_model.kc = _kc;
				var uiConf:XML = XML(_uiconf.confFile)
				_model.usersModel.usersUpgradeLink =  uiConf.upgradeUsersPage.text().toString();
				_model.usersModel.currentUserInfo = _userInfo;
				_model.rolesModel.partnerPermissionsUiconf = permissionManager.partnerUIDefinitions;
				// list partner permissions
				var cge:CairngormEvent = new ListItemsEvent(ListItemsEvent.LIST_PARTNER_PERMISSIONS);
				cge.dispatch();
				cge = new PartnerEvent(PartnerEvent.GET_PARTNER_DATA);
				cge.dispatch();
			}


			/**
			 * change url
			 * */
			private function onItemClick(event:Event):void {
				var subTab:IAdminSubtab = contentView.selectedChild as IAdminSubtab; 
				subTab.refreshData();
				var label:String = contentView.selectedChild.id;
				JSGate.writeUrlHash(Administration.NAME, label);
				switch (subTab) {
					case users:
						GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.PAGE_VIEW + GoogleAnalyticsConsts.ADMIN_USERS);
						break;
					case roles:
						GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.PAGE_VIEW + GoogleAnalyticsConsts.ADMIN_ROLES);
						break;
				}
			}


			/**
			 * event handler that allows the inner panels to enable or disable HTML headers
			 * */
			protected function enableHeaderHandler(event:GeneralEvent):void
			{
				var bEnable:Boolean = event.data;
				enableHtmlTabs(bEnable);
			}
			
			protected function onHelp():void {
				var anchor:String;
				if (contentView.selectedChild == users)
					anchor='section_admin_users';
				else
					anchor='section_admin_roles';
				
				dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, anchor));
			}

		]]>
	</mx:Script>
	<!-- the FrontController, containing Commands specific to this appliation -->
	<control:AdminController/>
	

	<mx:HBox width="100%" styleName="tabsContainer" enabled="{true}">
		<mx:TabBar id="secTln" styleName="tln" itemClick="onItemClick(event)" buttonMode="true"
				   dataProvider="contentView"/>
		<mx:Spacer width="100%"/>
		<mx:Button styleName="help" buttonMode="true" click="{onHelp()}"/>
	</mx:HBox>
	<mx:ViewStack id="contentView" width="100%" height="100%" styleName="adminViewstack"
				  horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<view:Users id="users" label="{resourceManager.getString('admin','users_tab')}" enableHeader="enableHeaderHandler(event)"
					width="100%" model="{_model.usersModel}" rolesModel="{_model.rolesModel}" userInfo="{_userInfo}" isLoading="{_model.isLoading}"/>
		<view:Roles id="roles" label="{resourceManager.getString('admin','roles_tab')}" enableHeader="enableHeaderHandler(event)"
					width="100%" model="{_model.rolesModel}" isLoading="{_model.isLoading}" />
	</mx:ViewStack>

</modules:KmcModule>
